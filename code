//@version=5
indicator(title='Alert Bot Scalping', overlay=true)

// Inputs
a = input(2, title='Key Value. \'This changes the sensitivity\'')
c = input(1, title='ATR Period')
h = input(false, title='Signals from Heikin Ashi Candles')

// Calculating ATR
xATR = ta.atr(c)
nLoss = a * xATR

// Calculating Heikin Ashi or Regular Candles
src = h ? request.security(ticker.heikinashi(syminfo.tickerid), timeframe.period, close, lookahead=barmerge.lookahead_off) : close

// Calculating Trailing Stop
xATRTrailingStop = 0.0
iff_1 = src > nz(xATRTrailingStop[1], 0) ? src - nLoss : src + nLoss
iff_2 = src < nz(xATRTrailingStop[1], 0) and src[1] < nz(xATRTrailingStop[1], 0) ? math.min(nz(xATRTrailingStop[1]), src + nLoss) : iff_1
xATRTrailingStop := src > nz(xATRTrailingStop[1], 0) and src[1] > nz(xATRTrailingStop[1], 0) ? math.max(nz(xATRTrailingStop[1]), src - nLoss) : iff_2

// Calculating Position
pos = 0
iff_3 = src[1] > nz(xATRTrailingStop[1], 0) and src < nz(xATRTrailingStop[1], 0) ? -1 : nz(pos[1], 0)
pos := src[1] < nz(xATRTrailingStop[1], 0) and src > nz(xATRTrailingStop[1], 0) ? 1 : iff_3

// Setting Color
xcolor = pos == -1 ? color.red : pos == 1 ? color.green : color.blue

// Calculating EMAs
ema4 = ta.ema(src, 4)
ema8 = ta.ema(src, 8)
ema21 = ta.ema(src, 21)

// Plotting EMAs
plot(ema4, color=color.new(color.orange, 0), title='EMA 4')
plot(ema8, color=color.new(color.purple, 0), title='EMA 8')
plot(ema21, color=color.new(color.yellow, 0), title='EMA 21')

// Calculating Buy and Sell Conditions
ema = ta.ema(src, 1)
above = ta.crossover(ema, xATRTrailingStop)
below = ta.crossover(xATRTrailingStop, ema)

buy = src > xATRTrailingStop and above
sell = src < xATRTrailingStop and below

barbuy = src > xATRTrailingStop
barsell = src < xATRTrailingStop

// Plotting Buy and Sell Signals
plotshape(buy, title='Buy', text='Buy', style=shape.labelup, location=location.belowbar, color=color.new(color.green, 0), textcolor=color.new(color.white, 0), size=size.tiny)
plotshape(sell, title='Sell', text='Sell', style=shape.labeldown, location=location.abovebar, color=color.new(color.red, 0), textcolor=color.new(color.white, 0), size=size.tiny)

// Coloring Bars
barcolor(barbuy ? color.green : na)
barcolor(barsell ? color.red : na)

// Setting up Alerts
alertcondition(buy, 'Alert Long', 'Alert Long')
alertcondition(sell, 'Alert Short', 'Alert Short')
